- add _init_ keyword to grammar


program: def
       | def program

def: varDef
   | functionDef
   | classDef

varDef: VAR ID ASSIGN expression SEMICOLON
      | VAR ID SEMICOLON

functionDef: DEFINE FUNCTION ID OPEN_PAREN optArgList CLOSE_PAREN block

classDef: DEFINE CLASS ID block

optArgList: argList
          | *empty*

argList: ID
       | ID COMMA argList

block: OPEN_BRACE statements optReturn CLOSE_BRACE

optReturn: returnStatement
         | *empty*

returnStatement: RETURN expression SEMICOLON

statements: statement
          | statement statements
          | *empty*

statement: expression SEMICOLON
         | ifStatement
         | whileLoop
         | forLoop
         | functionDef
         | BUNDLE SINGLE_QUOTE ID SINGLE_QUOTE

expression: unary
          | unary operator expression

exprList: expression
        | expression COMMA exprList

optExprList: exprList
           | *empty*

unary: ID
     | INTEGER
     | REAL
     | BOOLEAN
     | CHARACTER
     | STRING
     | NOT unary
     | OPEN_PAREN expression CLOSE_PAREN
     | UMINUS unary
     | ID OPEN_PAREN optExprList CLOSE_PAREN
     | ID PLUS PLUS
     | ID MINUS MINUS
     | ID PERIOD ID OPEN_PAREN optExprList CLOSE_PAREN
     | NEW UNDERSCORE ID UNDERSCORE OPEN_PAREN optExprList CLOSE_PAREN

operator: PLUS
        | TIMES
        | DIVIDE
        | MINUS
        | GREATER_THAN
        | LESS_THAN
        | EQUAL_TO
        | GT_EQUAL
        | LT_EQUAL
        | MODULO
        | PLUS_EQUAL
        | MINUS_EQUAL
        | ASSIGN

ifStatement: IF OPEN_PAREN expression CLOSE_PAREN block optElse

optElse: ELSE block
       | ELSE IF OPEN_PAREN expression CLOSE_PAREN block optElse
       | *empty*

whileLoop: WHILE OPEN_PAREN expression CLOSE_PAREN block

forLoop: FOR OPEN_PAREN expression SEMICOLON expression SEMICOLON expression CLOSE_PAREN block
